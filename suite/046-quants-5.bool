/**

Example for 5 quantifiers.

Quantifiers are indexed from 1 (left) to 5 (right).

universal(x)   ... x is a universal quantifier.
existential(x) ... x is an existential quantifier.
dependsOn(x,y) ... The domain definition of y depends on the variable
                   quantified over by x, where x < y (naturally).
                   Transitivity is ensured by the encoding.
related(x,y)   ... The variables that quantifiers x and y range over are
                   related, where x < y (symmetry breaking).
moveTo(x,y)    ... Quantifier x should be moved to position y.

**/

// - - - - - - INSTANCE - - - - - -

(universal(1) & existential(2) & universal(3) & existential(4) & universal(5))

(related(4,5))

(dependsOn(2,4) & dependsOn(4,5))

// - - - - - - ENCODING - - - - - -

// At one position there can be at most one quantifier.
(forall #p in [1...5] forall #q1 in [1...5] forall #q2 in [#q1+1...5]
    (moveTo(#q1,#p) -> ~moveTo(#q2,#p))
)

// Dependency is transitive.
(forall #ql in [1...5] forall #qm in [#ql+1...5] forall #qr in [#qm+1...5]
    (dependsOn(#ql,#qm) & dependsOn(#qm,#qr)) -> dependsOn(#ql,#qr)
)

// For any pair of quantifiers, the left one cannot depend on the right one.
(forall #ql in [1...5] forall #qr in [#ql+1...5] (~dependsOn(#qr,#ql)))

// Dependent quantifiers cannot move over its dependency.
// Related quantifiers cannot change their relative order.
(forall #ql in [1...5] forall #qr in [#ql+1...5]
    (
        (related(#ql,#qr) | dependsOn(#ql,#qr))
        ->
        ~(exists #l in [1...5] exists #r in [#l+1...5]
            moveTo(#ql,#r) & moveTo(#qr,#l)
        )
    )
)

// Every quantifier is either universal or existential.
(forall #q in [1...5] (universal(#q) ^ existential(#q)))

// Every quantifier must be moved to some position.
(forall #q in [1...5] exists #p in [1...5] moveTo(#q,#p))

// An existential quantifier that is placed to the
// left of a universal one is considered "bad".
(forall #ql in [1...5] forall #qr in [1...5]
    (forall #l in [1...5] forall #r in [#l+1...5]
        ((moveTo(#ql,#l) & moveTo(#qr,#r) & existential(#ql) & universal(#qr)) -> bad(#qr))
    )
)